(* The foreign function interface for accessing JavaScript. *)
(* :exemplar: platform js *)
class JS usingPlatform: platform internalKernel: ik = (
	|
	public undefined = Alien withIndex: 0.
	(* The top-level binding of `this`; in a web browser, this is the window object. *)
	public global = Alien withIndex: 4.
	private nextExpatIndex ::= 1.
	private messageLoop = ik messageLoop.
	|
) (
(* An Alien is a Newspeak proxy for a JavaScript object. Upon receiving a message, an Alien expatriates the arguments, sends the message to the target JavaScript object, and alienates the result.

An Expat is a JavaScript proxy for a Newspeak object. Upon receiving a message, an Expat alienates the arguments, sends the message to the target Newspeak object, and expatriates the result. *)
(* :exemplar: platform js global at: #Math *)
class Alien withIndex: i = (
	|
	public index <Integer> = i.
	|
) (
private asJSSelector: nsSelector <String> ^<String> = (
	| position = nsSelector indexOf: ':'. |
	0 = position ifTrue: [^nsSelector].
	^nsSelector copyFrom: 1 to: position - 1
)
(* JavaScript: this[key] *)
public at: key = (
	rawPushAlien: self index.
	rawPush: key.
	rawPerformGet.
	^rawPop
)
(* JavaScript: this[key] = value *)
public at: key put: value = (
	rawPushAlien: self index.
	rawPush: key.
	rawPush: value.
	rawPerformSet.
	^rawPop
)
protected doesNotUnderstand: message = (
	| jsSelector |
	0 = message arguments size ifTrue:
		[(message selector startsWith: 'isKindOf') ifTrue:
			[^false]].
	jsSelector:: asJSSelector: message selector.
	^#new = jsSelector
		ifTrue: [self newWithArguments: message arguments]
		ifFalse: [self perform: jsSelector withArguments: message arguments]
)
(* JavaScript: key in this *)
public includesKey: key ^<Boolean> = (
	rawPushAlien: self index.
	rawPush: key.
	rawPerformHas.
	^rawPop
)
(* JavaScript: this instanceof constructor *)
public instanceOf: constructor ^<Boolean> = (
	rawPushAlien: self index.
	rawPush: constructor.
	rawPerformInstanceOf.
	^rawPop
)
public isKindOfClosure ^<Boolean> = (
	^false
)
public isKindOfJSAlien ^<Boolean> = (
	^true
)
public isUndefined ^<Boolean> = (
	^0 = index
)
(* JavaScript: new this(arguments[0], ..., arguments[n-1]) *)
public newWithArguments: arguments <Array> = (
	rawPushAlien: self index.
	1 to: arguments size do:
		[:argumentIndex | rawPush: (arguments at: argumentIndex)].
	rawPerformNew: arguments size.
	^rawPop
)
(* JavaScript: this.selector(arguments[0], ..., arguments[n-1]) *)
public perform: selector <String> withArguments: arguments <Array> = (
	rawPushAlien: self index.
	rawPush: selector.
	1 to: arguments size do:
		[:argumentIndex | rawPush: (arguments at: argumentIndex)].
	rawPerformCall: arguments size.
	^rawPop
)
public printString ^<String> = (
	self isUndefined ifTrue: [^'undefined']. (* undefined.toString() throws in JS *)
	^self perform: #toString withArguments: {}
)
private pushExpat: closure = (
	| expatIndex |
	expatIndex:: nextExpatIndex.
	nextExpatIndex:: 1 + expatIndex.
	messageLoop handleMap at: expatIndex put:
		[:status :pending :count |
		| arguments = Array new: status. |
		status to: 1 by: -1 do:
			[:argumentIndex | arguments at: argumentIndex put: rawPop].
		rawPush: (closure valueWithArguments: arguments)].
	rawPushExpat: expatIndex.
)
private rawPerformCall: numArgs = (
	(* :pragma: primitive: 329 *)
	^Exception signal: rawPop printString
)
private rawPerformDelete = (
	(* :pragma: primitive: 328 *)
	^Exception signal: rawPop printString
)
private rawPerformGet = (
	(* :pragma: primitive: 326 *)
	^Exception signal: rawPop printString
)
private rawPerformHas = (
	(* :pragma: primitive: 332 *)
	^Exception signal: rawPop printString
)
private rawPerformInstanceOf = (
	(* :pragma: primitive: 331 *)
	^Exception signal: rawPop printString
)
private rawPerformNew: numArgs = (
	(* :pragma: primitive: 330 *)
	^Exception signal: rawPop printString
)
private rawPerformSet = (
	(* :pragma: primitive: 327 *)
	^Exception signal: rawPop printString
)
private rawPop = (
	(* :pragma: primitive: 323 *)
	^Alien withIndex: rawPopAgain
)
private rawPopAgain = (
	(* :pragma: primitive: 324 *)
	panic
)
private rawPush: object = (
	(* :pragma: primitive: 320 *)
	object isKindOfClosure
		ifTrue: [pushExpat: object]
		ifFalse: [rawPushAlien: object index]
)
private rawPushAlien: alienIndex = (
	(* :pragma: primitive: 321 *)
	panic
)
private rawPushExpat: expatIndex = (
	(* :pragma: primitive: 322 *)
	panic
)
(* JavaScript: delete this[key] *)
public removeKey: key = (
	rawPushAlien: self index.
	rawPush: key.
	rawPerformDelete.
	^rawPop
)
) : (
)
private panic = (
	(* :pragma: primitive: 187 *)
)
) : (
)
